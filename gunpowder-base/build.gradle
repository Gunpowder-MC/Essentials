archivesBaseName = "gunpowder-base"

dependencies {
    // Shading causes large dep issues
    compile project(":gunpowder-api")

    // Database
    // Don't shade for compat issues
    include libs.exposed_core
    include libs.exposed_jdbc
    include libs.exposed_java_time

    // Database drivers
    // No need to have these shaded
    include libs.postgresql
    include libs.mysql_connector_java
    include libs.sqlite_jdbc

    // Configs
    // Again, not shaded for compat
    include libs.jackson_core
    include libs.jackson_kotlin
    include libs.jackson_yaml
    include libs.jackson_databind
    include libs.jackson_annotations
    include libs.snakeyaml
    include 'org.slf4j:slf4j-api:1.8.0-beta1'  // TODO: Move to libs I'm fucking done adding deps

    // Dependency Injection
    include libs.guice
    include libs.javax_inject
    include libs.aop

    include libs.playerabilitylib
}

task remapShadowJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: shadowJar) {
    afterEvaluate {
        input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
        archiveName = "${archivesBaseName}-${version}.jar"
        addNestedDependencies = true
        remapAccessWidener = true
    }
}

jar {
    enabled = false
}

remapJar {
    enabled = false
}

shadowJar {
    enabled = true
    // Only shadow implementation/modImplementation
    configurations = [project.configurations.shade]
    classifier = "dev"
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
    afterEvaluate {
        input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
        archiveName = "${archivesBaseName}-${version}-maven.jar"
        addNestedDependencies = false
    }
}

build.dependsOn(shadowJar, remapShadowJar)

sourceSets {
    main {
        java.srcDir "${rootProject.rootDir}/gunpowder-api/src/main/java"
        kotlin.srcDir "${rootProject.rootDir}/gunpowder-api/src/main/kotlin"
    }
}
